! Left Panel

  script LeftPanel

  import div Container

  div Inner
  div Block1
  div Weather
  div FBDiv
  div Padding
  img EditButton
  variable Content
  variable Mode
  variable FacebookDone
  variable WeatherDone

  create Inner in Container
  set the style of Inner to `margin:5% 5%`

  create EditButton in Inner
  set the style of EditButton to `width:25px;display:none`
  set attribute `src` of EditButton to `https://` cat the hostname cat `/resources/system/edit.png`
  set attribute `title` of EditButton to `Edit the left sidebar article`
  on click EditButton send `editleft` to parent

  create Block1 in Inner
  set the style of Block1 to
    `padding-bottom:1em;border-bottom:1px solid lightgray;margin-bottom:1em`

  on message go to Show
  set ready
  stop

Show:
  get Mode from storage as `mode`
  if Mode is `admin` set style `display` of EditButton to `block`
  else set style `display` of EditButton to `none`
  rest get Content from `/resources/php/rest.php/_/nl_articles/slug/left?v=` cat now
  or begin
    alert `Failed to get the content of the left panel.`
    stop
  end
  set the content of Block1 to decode property `article` of Content

  if not FacebookDone
  begin
    create FBDiv in Inner
    set the style of FBDiv to `padding-bottom:1em;border-bottom:1px solid lightgray`
    rest get Content from `/resources/php/rest.php/_load/html~facebook?v=` cat now
    set the content of FBDiv to Content
    create Padding in Inner
    set the content of Padding to `&nbsp;`
  end

  if not WeatherDone
  begin
    create Weather in Inner
    set attribute `id` of Weather to `cont_d8f5d58c339aa6fbea3a58d369d5c0b9`
    require js `https://www.yourweather.co.uk/wid_loader/d8f5d58c339aa6fbea3a58d369d5c0b9`
    set WeatherDone
  end
  stop