! Menu

  script Menu

  import div Header and variable Mobile

  div MenuDiv
  div SectionDiv
  div ReleaseButton
  div HomeButton
  div AboutButton
  div SectionsButton
  div UserButton
  div UserPanel
  div AddButton
  select SectionSelect
  img ReleaseIcon
  img UserIcon
  img AddIcon
  module UserModule
  variable Script
  variable Style
  variable Style2
  variable Message
  variable UserName
  variable List
  variable Sections
  variable Section
  variable Name
  variable Count
  variable Record
  variable SelectedSection
  variable SectionEnable
  variable Action
  variable Value
  variable N
  
!  debug step

  get UserName from storage as `user.name`
  set SectionEnable
  
  create UserPanel in Header

  create MenuDiv in Header
  if Mobile set the style of MenuDiv to
    `display:flex;width:100%`
  else set the style of MenuDiv to
    `display:flex;width:800px`
  set style `display` of MenuDiv to `flex`
  set style `margin` of MenuDiv to `0 auto`
  set style `padding` of MenuDiv to `1em 0`
  set style `text-align` of MenuDiv to `center`

  put `flex:1;background:#444;padding-top:0.5em;color:white;font-weight:bold` into Style
  put `width:100%;background:#444;color:white;font-weight:bold` into Style2
  
  create ReleaseButton in MenuDiv
  set the style of ReleaseButton to `width:1em;background:#444;padding:0.5em 0 0.5em 0.5em;text-align:left`
  create ReleaseIcon in ReleaseButton
  set style `width` of ReleaseIcon to `1em`
  set style `height` of ReleaseIcon to `1em`
  set attribute `src` of ReleaseIcon to `/resources/icon/refresh.png`
  set style `cursor` of ReleaseIcon to `pointer`
  set attribute `title` of ReleaseIcon to `Show release requests`
  on click ReleaseIcon go to ShowWaiting
  gosub to CheckIfAdmin
  
  create HomeButton in MenuDiv
  set the style of HomeButton to Style
  gosub to DisableHome
  set the content of HomeButton to `Home`
  on click HomeButton go to DoHome
  
  create AboutButton in MenuDiv
  set the style of AboutButton to Style
  set style `cursor` of AboutButton to `pointer`
  set the content of AboutButton to `About`
  set attribute `title` of AboutButton to `About this website`
  on click AboutButton go to DoAbout

  create SectionDiv in MenuDiv
  set the style of SectionDiv to Style
  create SectionsButton in SectionDiv
  set the style of SectionsButton to Style2
  set style `cursor` of SectionsButton to `pointer`
  set the content of SectionsButton to `Sections`
  set attribute `title` of SectionsButton to `Choose a section`
  on click SectionsButton gosub to DoSections
  create SectionSelect in SectionDiv
  set the style of SectionSelect to Style
  set style `display` of SectionSelect to `none`
  put empty into SelectedSection
  
  create AddButton in MenuDiv
  if UserName is not empty
  begin
    set the style of AddButton to `width:1.5em;background:#444;padding:0.5em 0;text-align:left`
    create AddIcon in AddButton
    set style `width` of AddIcon to `1em`
    set style `height` of AddIcon to `1em`
    set attribute `src` of AddIcon to `/resources/icon/plus.png`
    if UserName is empty set style `display` of AddButton to `none`
    set attribute `title` of AddIcon to `Add a new article`
    on click AddIcon go to AddArticle
    if UserName is empty set style `display` of AddButton to `none`
    else set style `display` of AddButton to `inline-block`
  end

  create UserButton in MenuDiv
  set the style of UserButton to `width:1.5em;background:#444;padding:0.5em 0;text-align:left`
  create UserIcon in UserButton
  set style `width` of UserIcon to `1em`
  set style `height` of UserIcon to `1em`
  if UserName is empty set attribute `src` of UserIcon to `/resources/icon/user.png`
  else set attribute `src` of UserIcon to `/resources/icon/user-loggedin.png`
  set style `cursor` of UserIcon to `pointer`
  set attribute `title` of UserIcon to `Login/logout`
  on click UserIcon go to ManageUsers

  on message
  begin
    put the message into Message
!alert `Menu message: ` cat Message
	if Message is `login`
    begin
    	send Message to parent
        gosub to CheckIfAdmin
    end
	else if Message is `logout`
    begin
    	send Message to parent
        gosub to CheckIfAdmin
    end
    else if Message is `reset`
    begin
    	put empty into SelectedSection
        gosub to DoSections
  		gosub to ResetSections
        set the content of HomeButton to `Home`
        gosub to EnableHome
    end
    else if Message is `hide`
    begin
        set style `display` of MenuDiv to `none`
    end
    else if Message is `show`
    begin
        set style `display` of MenuDiv to `flex`
    end
    else
    begin
      put property `action` of Message into Action
      if Action is `enable`
      begin
        if property `button` of Message is `home` gosub to EnableHome
        else if property `button` of Message is `sections` gosub to EnableSection
      end
      else if Action is `disable`
      begin
        if property `button` of Message is `home` gosub to DisableHome
        else if property `button` of Message is `sections` gosub to DisableSection
      end
      else if Action is `home`
      begin
      	set the content of HomeButton to property `name` of Message
        set attribute `title` of HomeButton to property `title` of Message
      end
    end
  end

  set ready
  stop

CheckIfAdmin:
	get UserName from storage as `user.name`
    if UserName is empty set style `display` of ReleaseButton to `none`
    else
    begin
    	rest get Record from  `name/` cat UserName cat `?v=` cat now
    	if property `admin` of Record is 1 set style `display` of ReleaseButton to `inline-block`
    	else set style `display` of ReleaseButton to `none`
    end
	return

EnableHome:
    set style `cursor` of HomeButton to `pointer`
    set style `color` of HomeButton to `white`
	return

DisableHome:
    set style `cursor` of HomeButton to `none`
    set style `color` of HomeButton to `gray`
	return

EnableSection:
    set style `cursor` of SectionsButton to `pointer`
    set style `color` of SectionsButton to `white`
    set SectionEnable
	return

DisableSection:
    set style `cursor` of SectionsButton to `none`
    set style `color` of SectionsButton to `gray`
    clear SectionEnable
	return

ResetSections:
  	set style `display` of SectionsButton to `inline-block`
  	set style `display` of SectionSelect to `none`
    return

EnableFilters:
	gosub to EnableSection
    go to EnableHome

DisableFilters:
	gosub to DisableSection
    goto DisableHome

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Show waiting releases
ShowWaiting:
	send `waiting` to parent
    stop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Manage users
ManageUsers:
	rest get Script from `/resources/ecs/user.ecs?v=` cat now
    run Script with UserPanel and Mobile as UserModule
    clear UserPanel
    get UserName from storage as `user.name`
    if UserName is empty
    begin
    	set attribute `src` of UserIcon to `/resources/icon/user.png`
        set style `display` of AddButton to `none`
    end
    else
    begin
    	set attribute `src` of UserIcon to `/resources/icon/user-loggedin.png`
        set style `display` of AddButton to `inline-block`
    end
    stop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Add a new article
AddArticle:
	send `new` to parent
	stop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Here when the Home button is clicked
DoHome:
	send `back` to parent
    stop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Here when the About button is clicked
DoAbout:
	send `about` to parent
    stop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Show the list of sections
DoSections:
  if not SectionEnable stop
  rest get Sections from `/resources/php/rest.php/_/sections/enum?v=` cat now
  put the json count of Sections into Count
  set the elements of Section to Count
  put `[]` into List
  json add `all` to List
  put 0 into N
  while N is less than Count
  begin
    index Section to N
    put element N of Sections into Section
    put Section into Name
    replace `%20` with ` ` in Name
   	json add Name to List
    add 1 to N
  end
  put SelectedSection into Value
  if Value is empty put `all` into Value
  set SectionSelect from List as Value
  set style `display` of SectionsButton to `none`
  set style `display` of SectionSelect to `inline-block`
  on change SectionSelect
  begin
   	put attribute `value` of SectionSelect into SelectedSection
    if SelectedSection is `all` put empty into SelectedSection
   	put `` into Message
   	set property `source` of Message to `Menu`
   	set property `selector` of Message to `section`
   	set property `section` of Message to SelectedSection
    gosub to ResetSections
	gosub to EnableHome
   	send Message to parent
  end
  return